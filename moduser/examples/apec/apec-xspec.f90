      ! ===========================================================
      ! APEC/XSPEC interface for the SPEX user model
      !
      ! Usage:
      !   Edit the program below to your wishes in two steps:
      !
      !   STEP 1 (Optional):
      !     Change the APECROOT path to point to your ATOMDB 
      !     installation.
      !
      !   STEP 3 (Optional):
      !     Adapt the TCL script that is generated by this program
      !     to your needs. The lines to change are clearly marked
      !     in the source code. Please do not change the other
      !     lines, unless you know what you are doing.
      ! ===========================================================
      program apecxspec
      use moduser
      implicit none
      
      real,allocatable :: ene(:), flux(:)
      integer          :: i,j,tot,status
      character*256    :: apecroot,path


      ! ===========================================================
      ! STEP 1: Set APECROOT path (Optional)
      ! This is the path where you installed the ATOMDB APEC table
      ! ===========================================================
      status = 0
      call get_environment_variable('ATOMDB',apecroot,tot,status,.true.) 
      
      if (status.ne.0) then
        write(*,*) 'Please set the ATOMDB environment variable is you want a recent APEC model.'
      endif
      
      
      ! ===========================================================
      ! STEP 2: Get the input values from SPEX
      ! ===========================================================

      ! Get input and output filenames for SPEX user model
      call getfilenames(fin,fout,ier)

      ! Read input file from SPEX
      call readprm(trim(fin))

      ! Write energy grid to XSpec file
      open(2,file="energies.txt",status="unknown")
      write(2,'(E15.7)') ipar%egb(1) - ipar%deg(1)
      do i=1,ipar%neg
        write(2,'(E15.7)') ipar%egb(i)
      enddo
      close(2)
      
      ! ===========================================================
      ! STEP 3: Create TCL script for APEC
      !
      ! See XSPEC manual for suggestions:
      ! https://heasarc.gsfc.nasa.gov/xanadu/xspec/manual/XSappendixTcl.html
      ! ===========================================================
      open(1,file="apec.tcl",status="unknown")
      write(path,'("xset APECROOT ",a)') trim(apecroot)
      
      ! Uncomment next line if you do not want to use default ATOMDB
      !write(1,*) path
      
      write(1,*) "model bvvapec & ", ipar%par(1), " & ", &
          ipar%par(2), " & ", ipar%par(3), " & ",      &
          ipar%par(4), " & ", ipar%par(5), " & ",      &
          ipar%par(6), " & ", ipar%par(7), " & ",      &
          ipar%par(8), " & ", ipar%par(9), " & ",      &
	  ipar%par(10), " & ", ipar%par(11), " & ",      &
          ipar%par(12), " & ", ipar%par(13), " & ",      &
	  ipar%par(14), " & ", ipar%par(15), " & ",      &
          ipar%par(16), " & ", ipar%par(17), " & ",      &
          ipar%par(18), " & ", ipar%par(19), " & ",      &
          ipar%par(20), " & ", ipar%par(21), " & ",      &
          ipar%par(22), " & ", ipar%par(23), " & ",      &
          ipar%par(24), " & ", ipar%par(25), " & ",      &
	  ipar%par(26), " & ", ipar%par(27), " & ",      &
          ipar%par(28), " & ", ipar%par(29), " & ",      &
	  ipar%par(30), " & ", ipar%par(31), " & ",      &
          ipar%par(32), " & ", ipar%par(33), " & ",      &
	  ipar%par(34) 	  	  
      write(1,*) "energies energies.txt"
      write(1,*) "abund aspl"
      write(1,*) "set ff [open user.dat a]"
      write(1,*) "tclout plot model x"
      write(1,*) "puts $ff $xspec_tclout"
      write(1,*) "tclout plot model y"
      write(1,*) "puts $ff $xspec_tclout"
      write(1,*) "quit"
      close(1)


      ! ===========================================================
      ! STEP 4: Run Xspec tcl script and read output
      ! ===========================================================
      call system("rm -rf user.dat")
      call system("xspec apec.tcl >> /dev/null")
      
      ! Allocate output arrays
      call allopar()
      ! Xspec returns one bin less...(?)
      allocate(ene(ipar%neg))
      allocate(flux(ipar%neg))
      
      ! Read Xspec output
      open(10,file="user.dat",status="old")
      read(10,*) ene
      read(10,*) flux
      close(10)
      
      
      ! ===========================================================
      ! STEP 5: Return the output to SPEX and clean up
      ! ===========================================================
      
      ! Do not use wener values for now
      do i=1,opar%neg - 1
        opar%sener(i)=flux(i)*ipar%deg(i)
        opar%wener(i)=0.0
      enddo
      opar%sener(opar%neg) = 0.
     
      ! Write result to output file
      call writespc(fout)
      
      ! Clean up memory and remove temporary files 
      call deallpar()
      deallocate(ene)
      deallocate(flux)
      call system("rm -rf user.dat")
      call system("rm -rf apec.tcl")
     
      end program apecxspec
   
